apply plugin: 'c'

apply from: 'platform-config.gradle'

def genSrc = "${buildDir}/src"
task extractCUnit(type: Copy) {
    from tarTree("CUnit-2.1-2-src.tar.bz2")
    into "${genSrc}"
    include "CUnit-2.1-2/CUnit/**"
    exclude "**/Makefile*"
}

model {
    components {
        cunit(NativeLibrarySpec) {
            sources {
                c {
                    source {
                        srcDir "${genSrc}/CUnit-2.1-2/CUnit/Sources"
                        exclude "**/Curses/*"
                        exclude "**/Test/*"
                        exclude "**/Win/*"
                    }
                    exportedHeaders {
                        srcDir "${genSrc}/CUnit-2.1-2/CUnit/Headers"
                    }
                    builtBy tasks.extractCUnit
                }
            }
        }
    }
    tasks {
        buildStaticLibs(Task) {
            dependsOn $.binaries.withType(StaticLibraryBinarySpec).findAll {
                it.buildable
            }
        }
        publish(Copy) {
            dependsOn "extractCUnit"
            dependsOn "buildStaticLibs"
            group "Publication"
            into "../publish"
            from("${genSrc}/CUnit-2.1-2/CUnit/Headers") {
                into "headers/CUnit"
				// This file is excluded as it was patch to fix issue:
				// https://sourceforge.net/p/cunit/bugs/71/
				exclude "CUnit.h"
            }
            from("${buildDir}/libs") {
                into "lib"
            }
        }
    }
}

