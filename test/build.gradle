apply plugin: "c"
apply plugin: "cunit"

apply from: "../cunit/platform-config.gradle"

def cunitDir = "../publish"

model {
    repositories {
        libs(PrebuiltLibraries) {
            cunit {
                headers.srcDir "${cunitDir}/headers"
                binaries.withType(StaticLibraryBinary) { binary ->
                    def libName = binary.targetPlatform.operatingSystem.windows ? "cunit.lib" : "libcunit.a"
                    binary.staticLibraryFile = file("${cunitDir}/lib/cunit/static/${binary.targetPlatform.name}/${libName}")
                }
            }
        }
    }
    components {
        math(NativeLibrarySpec)
    }
    binaries {
        withType(TestSuiteBinarySpec) {
            lib library: "cunit", linkage: "static"
        }
    }
    tasks {
        unitTest(Task) {
            dependsOn $.binaries.withType(TestSuiteBinarySpec)
                .findAll { it.buildable }
                // TODO: This should be it.tasks.run, which should use the install image of the test exe
                .collect { it.namingScheme.getTaskName("run") }
            group "Verification"
        }
    }
}

